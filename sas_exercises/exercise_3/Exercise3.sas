/**********************************************************************************

PROGRAM:      EXERCISE3.SAS

DESCRIPTION:  THIS PROGRAM ILLUSTRATES HOW TO POOL MEPS DATA FILES FROM DIFFERENT YEARS
              THE EXAMPLE USED IS POPULATION AGE 26-30 WHO ARE UNINSURED BUT HAVE HIGH INCOME

	         DATA FROM 2015 AND 2016 ARE POOLED.

              VARIABLES WITH YEAR-SPECIFIC NAMES MUST BE RENAMED BEFORE COMBINING FILES.  
              IN THIS PROGRAM THE INSURANCE COVERAGE VARIABLES 'INSCOV15' AND 'INSCOV16' ARE RENAMED TO 'INSCOV'.

	         SEE HC-036 (1996-2015 POOLED ESTIMATION FILE) FOR
              INSTRUCTIONS ON POOOLING AND CONSIDERATIONS FOR VARIANCE
	         ESTIMATION FOR PRE-2002 DATA.

INPUT FILE:     (1) C:\DATA\H192.SAS7BDAT (2016 FULL-YEAR FILE)
	            (2) C:\DATA\H181.SAS7BDAT (2015 FULL-YEAR FILE)

*********************************************************************************/;
/* IMPORTANT NOTE: Use the next 6 lines of code, only if you want to specify an 
alternative destination for SAS log and SAS procedure output. 
Otherwise comment  out those six statements */

%LET MyFolder= S:\CFACT\Shared\WORKSHOPS\2019\Spring2019\Exercise_3;
OPTIONS LS=132 PS=79 NODATE FORMCHAR="|----|+|---+=|-/\<>*" PAGENO=1;
FILENAME MYLOG "&MyFolder\Exercise3_log.TXT";
FILENAME MYPRINT "&MyFolder\Exercise3_OUTPUT.TXT";
PROC PRINTTO LOG=MYLOG PRINT=MYPRINT NEW;
RUN;

proc datasets lib=work nolist kill; quit; /* delete  all files in the WORK library */
LIBNAME CDATA 'C:\DATA';
OPTIONS NODATE;
TITLE1 '2019 AHRQ MEPS DATA USERS WORKSHOP (EXERCISE3.SAS)';
TITLE2 'COMBINED MEPS DATA FROM 2015 and 2016';

PROC FORMAT;
	VALUE POVCAT 
    1 = '1 POOR/NEGATIVE'
    2 = '2 NEAR POOR'
    3 = '3 LOW INCOME'
    4 = '4 MIDDLE INCOME'
    5 = '5 HIGH INCOME'
    ;

	VALUE INSF
	1 = '1 ANY PRIVATE'
	2 = '2 PUBLIC ONLY'
	3 = '3 UNINSURED';

    VALUE AGE
    26-30='26-30'
    0-25='0-25'
    31-HIGH='31+';

	VALUE  SUBPOP (max= 50)
	1 = 'AGE 26-30, UNINSURED WHOLE YEAR, AND HIGH INCOME'
	2 ='OTHERS';
run;


/* KEEP THE SPECIFIED VARIABLES WHEN READING THE INPUT DATA SET AND
   RENAME YEAR SPECIFIC VARIABLES PRIOR TO COMBINING FILES */

DATA WORK.YR2015;
	SET CDATA.H181 
       (KEEP= DUPERSID INSCOV15 PERWT15F VARSTR VARPSU POVCAT15 AGELAST TOTSLF15
        RENAME=(INSCOV15=INSCOV PERWT15F=PERWT POVCAT15=POVCAT TOTSLF15=TOTSLF));
RUN;

DATA WORK.YR2016;
	SET CDATA.H192 
    (KEEP= DUPERSID INSCOV16 PERWT16F VARSTR VARPSU POVCAT16 AGELAST TOTSLF16
     RENAME=(INSCOV16=INSCOV PERWT16F=PERWT POVCAT16=POVCAT TOTSLF16=TOTSLF));
 run;

 /* CONCATENATE YR1 AND YR2 DATA SETS */

DATA WORK.POOL;
     SET WORK.YR2015 WORK.YR2016 INDSNAME=source;
	 
	 /* Create a YEAR Variable for checking data*/
	 year=SUBSTR(source, LENGTH(source)-3);

     POOLWT = PERWT/2 ;  /* Pooled survey weight */

     /*Create a dichotomous SUBPOP variable 
	   (POPULATION WITH AGE=26-30, UNINSURED WHOLE YEAR, AND HIGH INCOME)
	 */
     IF 26 LE AGELAST LE 30 AND POVCAT=5 AND INSCOV=3 THEN SUBPOP=1;
     ELSE SUBPOP=2;
     
RUN;

ODS HTML CLOSE; /*This will make the default HTML output no longer active,
                  and the output will not be displayed in the Results Viewer.*/
ODS LISTING ;  /*Open the listing destination */

TITLE1 ' ';
TITLE2 ' ';
TITLE3 'SUPPORTING CROSSTAB FOR THE CREATION OF THE SUBPOP FLAG';
PROC SORT DATA=WORK.POOL; BY YEAR SUBPOP; RUN;
PROC FREQ DATA=WORK.POOL;
    BY YEAR SUBPOP;
   	TABLES AGELAST*POVCAT*INSCOV/ LIST MISSING ;
	TABLES POVCAT*INSCOV/ LIST MISSING ;
	FORMAT AGELAST AGE. POVCAT POVCAT. INSCOV INSF.;
RUN;
TITLE3 "CHECK MISSING VALUES ON THE COMBINED DATA";
PROC MEANS DATA=POOL N NMISS;
RUN;


ods graphics off; /*Suppress the graphics */
TITLE3 'WEIGHTED ESTIMATE ON TOTSLF FOR COMBINED DATA W/AGE 26-30, UNINSURED WHOLE YEAR, AND HIGH INCOME';
TITLE4 "AUTOMATIC OUTPUT GENERATED FROM PROC SURVEYMEANS";

PROC SURVEYMEANS DATA=WORK.POOL NOBS MEAN STDERR;
	STRATUM VARSTR ;
	CLUSTER VARPSU ;
	WEIGHT  POOLWT;
	VAR  TOTSLF;
	DOMAIN  SUBPOP("AGE 26-30, UNINSURED WHOLE YEAR, AND HIGH INCOME");
    FORMAT SUBPOP SUBPOP.;
	ODS OUTPUT DOMAIN=work.domain_results;
RUN;

TITLE4 "CUSTOMIZED OUTPUT BASED ON ODS TABLE NAME (DOMAIN OUTPUT DATA SET) CREATED FROM PROC SURVEYMEANS";
proc print data= work.domain_results noobs split='*';
 var   VARLABEL N  mean StdErr  ;
 label mean = 'Mean'
       StdErr = 'SE of Mean'
       TOTSLF='TOTAL AMT PAID BY SELF/FAMILY';;
       format N Comma12. mean comma9.1 stderr 9.4;             
run;
/* THE PROC PRINTTO null step is required to close the PROC PRINTTO, 
 only if used earlier */
PROC PRINTTO;
RUN;
 
