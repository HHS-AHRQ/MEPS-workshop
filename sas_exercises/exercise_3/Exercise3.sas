/**********************************************************************************

PROGRAM:      EXERCISE3.SAS

DESCRIPTION:  THIS PROGRAM ILLUSTRATES HOW TO POOL MEPS DATA FILES FROM DIFFERENT YEARS
              THE EXAMPLE USED IS POPULATION AGE 26-30 WHO ARE UNINSURED BUT HAVE HIGH INCOME

	         DATA FROM 2016 AND 2017 ARE POOLED.

              VARIABLES WITH YEAR-SPECIFIC NAMES MUST BE RENAMED BEFORE COMBINING FILES.  
              IN THIS PROGRAM THE INSURANCE COVERAGE VARIABLES 'INSCOV15' AND 'INSCOV16' ARE RENAMED TO 'INSCOV'.

	         SEE HC-036 (1996-2016 POOLED ESTIMATION FILE) FOR
              INSTRUCTIONS ON POOOLING AND CONSIDERATIONS FOR VARIANCE
	         ESTIMATION FOR PRE-2002 DATA.

INPUT FILE:     (1) C:\DATA\H201.SAS7BDAT (2017 FULL-YEAR FILE)
	            (2) C:\DATA\H192.SAS7BDAT (2016 FULL-YEAR FILE)

*********************************************************************************/;
/* IMPORTANT NOTES: Use the next 6 lines of code, if you want to specify an alternative destination for SAS log and 
SAS procedure output.*/

%LET MyFolder= S:\CFACT\Shared\WORKSHOPS\2019\Fall2019\SAS\Exercise_3;
OPTIONS LS=132 PS=79 NODATE FORMCHAR="|----|+|---+=|-/\<>*" PAGENO=1;
FILENAME MYLOG "&MyFolder\Exercise3_log.TXT";
FILENAME MYPRINT "&MyFolder\Exercise3_OUTPUT.TXT";
PROC PRINTTO LOG=MYLOG PRINT=MYPRINT NEW;
RUN;

proc datasets lib=work nolist kill; quit; /* delete  all files in the WORK library */
LIBNAME CDATA 'C:\DATA';
OPTIONS NODATE;
TITLE1 '2019 AHRQ MEPS DATA USERS WORKSHOP (EXERCISE3.SAS)';
TITLE2 'POOL MEPS DATA FILES FROM DIFFERENT YEARS (2016 and 2017)';

PROC FORMAT;
	VALUE POVCAT 
    1 = '1 POOR/NEGATIVE'
    2 = '2 NEAR POOR'
    3 = '3 LOW INCOME'
    4 = '4 MIDDLE INCOME'
    5 = '5 HIGH INCOME'
    ;

	VALUE INSF
	1 = '1 ANY PRIVATE'
	2 = '2 PUBLIC ONLY'
	3 = '3 UNINSURED';

    VALUE AGE
    26-30='26-30'
    0-25='0-25'
    31-HIGH='31+';
run;

/* FREQUENCY OF 2016 */
DATA YR1;
	SET CDATA.H192 (KEEP= DUPERSID INSCOV16 PERWT16F VARSTR VARPSU POVCAT16 AGELAST TOTSLF16);
     IF PERWT16F>0;
RUN;

TITLE3 'UNWEIGHTED FREQUENCY FOR 2016 FY PERSONS WITH AGE 26-30';
PROC FREQ DATA= YR1 (WHERE=(26 LE AGELAST LE 30));
	TABLES POVCAT16*INSCOV16/ LIST MISSING ;
	FORMAT INSCOV16 INSF.  POVCAT16 POVCAT.;
RUN;

/* FREQUENCY OF 2017*/
DATA YR2;
	SET CDATA.H201 (KEEP= DUPERSID INSCOV17 PERWT17F VARSTR VARPSU POVCAT17 AGELAST TOTSLF17);
     IF PERWT17F>0;
run;

TITLE3 'UNWEIGHTED FREQUENCY FOR 2017 FY PERSONS WITH AGE 26-30';
PROC FREQ DATA= YR2 (WHERE=(26 LE AGELAST LE 30));
	TABLES POVCAT17*INSCOV17/ LIST MISSING ;
	FORMAT INSCOV17 INSF.  POVCAT17 POVCAT.;
RUN;


/* RENAME YEAR SPECIFIC VARIABLES PRIOR TO COMBINING FILES */
DATA YR1X;
	SET YR1 (RENAME=(INSCOV16=INSCOV PERWT16F=PERWT POVCAT16=POVCAT TOTSLF16=TOTSLF));
RUN;

DATA YR2X;
	SET YR2 (RENAME=(INSCOV17=INSCOV PERWT17F=PERWT POVCAT17=POVCAT TOTSLF17=TOTSLF));
RUN;

DATA WORK.POOL;
     LENGTH INSCOV AGELAST POVCAT VARSTR VARPSU 8;
	SET WORK.YR1X WORK.YR2X;
     POOLWT = PERWT/2 ;
   
     IF 26 LE AGELAST LE 30 AND POVCAT=5 AND INSCOV=3 THEN SUBPOP=1;
     ELSE SUBPOP=2;

     LABEL SUBPOP='POPULATION WITH AGE=26-30, UNINSURED WHOLE YEAR, AND HIGH INCOME'
           TOTSLF='TOTAL AMT PAID BY SELF/FAMILY';
RUN;
OPTIONS PS=132;
TITLE3 "CHECK MISSING VALUES ON THE COMBINED DATA";
PROC MEANS DATA=POOL N NMISS;
RUN;

TITLE3 'SUPPORTING CROSSTAB FOR THE CREATION OF THE SUBPOP FLAG';
PROC FREQ DATA=WORK.POOL;
	TABLES SUBPOP SUBPOP*AGELAST*POVCAT*INSCOV/ LIST MISSING ;
	FORMAT  AGELAST AGE. ;
RUN;

ODS SELECT SUMMARY DOMAIN;
TITLE3 'WEIGHTED ESTIMATE ON TOTSLF FOR COMBINED DATA W/AGE=26-30, UNINSURED WHOLE YEAR, AND HIGH INCOME';
PROC SURVEYMEANS DATA=WORK.POOL NOBS MEAN STDERR;
	STRATUM VARSTR ;
	CLUSTER VARPSU ;
	WEIGHT  POOLWT;
	VAR  TOTSLF;
	DOMAIN  SUBPOP("1");
	ODS OUTPUT DOMAIN=work.domain_results;
RUN;

ODS LISTING;
TITLE3 'WEIGHTED ESTIMATE ON TOTSLF FOR COMBINED DATA W/AGE=26-30, UNINSURED WHOLE YEAR, AND HIGH INCOME';
proc print data= work.domain_results noobs split='*';
 var   VARLABEL N  mean StdErr  ;
 label mean = 'Mean'
       StdErr = 'SE of Mean';
       format N Comma12. mean stderr comma9.;             
run;
/* THE PROC PRINTTO null step is required to close the PROC PRINTTO */
PROC PRINTTO;
RUN;
 
