/**********************************************************************************

PROGRAM:      EXERCISE3.SAS

DESCRIPTION:  THIS PROGRAM ILLUSTRATES HOW TO POOL MEPS DATA FILES FROM DIFFERENT YEARS
              THE EXAMPLE USED IS POPULATION AGE 26-30 WHO ARE UNINSURED BUT HAVE HIGH INCOME

	         DATA FROM 2016 AND 2017 ARE POOLED.

              VARIABLES WITH YEAR-SPECIFIC NAMES MUST BE RENAMED BEFORE COMBINING FILES.  
              IN THIS PROGRAM THE INSURANCE COVERAGE VARIABLES 'INSCOV15' AND 'INSCOV16' ARE RENAMED TO 'INSCOV'.

	         SEE HC-036 (1996-2016 POOLED ESTIMATION FILE) FOR
              INSTRUCTIONS ON POOLING AND CONSIDERATIONS FOR VARIANCE
	         ESTIMATION FOR PRE-2002 DATA.

INPUT FILE:     (1) C:\DATA\H192.SAS7BDAT (2016 FULL-YEAR FILE)
	            (2) C:\DATA\H201.SAS7BDAT (2017 FULL-YEAR FILE)

*********************************************************************************/;
/*  IMPORTANT NOTE:  Use the next 6 lines of code, only if you want SAS to create 
    separate files for SAS log and output.  Otherwise comment  out those 6 lines */

%LET MyFolder= S:\CFACT\Shared\WORKSHOPS\2020\April2020\sas_exercises\Exercise_3;
OPTIONS LS=132 PS=79 NODATE FORMCHAR="|----|+|---+=|-/\<>*" PAGENO=1;
FILENAME MYLOG "&MyFolder\Exercise3_log.TXT";
FILENAME MYPRINT "&MyFolder\Exercise3_OUTPUT.TXT";
PROC PRINTTO LOG=MYLOG PRINT=MYPRINT NEW;
RUN;

proc datasets lib=work nolist kill; quit; /* delete  all files in the WORK library */
LIBNAME CDATA 'C:\DATA';
OPTIONS NODATE;
TITLE1 '2020 AHRQ MEPS DATA USERS WORKSHOP (EXERCISE3.SAS)';
TITLE2 'COMBINED MEPS DATA FROM 2016 and 2017';

PROC FORMAT;
	VALUE POVCAT 
    1 = '1 POOR/NEGATIVE'
    2 = '2 NEAR POOR'
    3 = '3 LOW INCOME'
    4 = '4 MIDDLE INCOME'
    5 = '5 HIGH INCOME'
    ;

	VALUE INSF
	1 = '1 ANY PRIVATE'
	2 = '2 PUBLIC ONLY'
	3 = '3 UNINSURED';

    VALUE AGE
    26-30='26-30'
    0-25='0-25'
    31-HIGH='31+';

	VALUE  SUBPOP (max= 50)
	1 = 'AGE 26-30, UNINSURED WHOLE YEAR, AND HIGH INCOME'
	2 ='OTHERS';
run;


/* KEEP THE SPECIFIED VARIABLES WHEN READING THE INPUT DATA SET AND
   RENAME YEAR SPECIFIC VARIABLES PRIOR TO COMBINING FILES */

DATA WORK.YR2016;
	SET CDATA.H192 
       (KEEP= DUPERSID INSCOV16 PERWT16F VARSTR VARPSU POVCAT16 AGELAST TOTSLF16
        RENAME=(INSCOV16=INSCOV PERWT16F=PERWT POVCAT16=POVCAT TOTSLF16=TOTSLF));
RUN;

DATA WORK.YR2017;
	SET CDATA.H201 
    (KEEP= DUPERSID INSCOV17 PERWT17F VARSTR VARPSU POVCAT17 AGELAST TOTSLF17
     RENAME=(INSCOV17=INSCOV PERWT17F=PERWT POVCAT17=POVCAT TOTSLF17=TOTSLF));
 run;

 /* CONCATENATE YR2016 AND YR2017 DATA SETS */

DATA WORK.POOL;
     SET WORK.YR2016 WORK.YR2017 INDSNAME=source;
	 
	 /* Create a YEAR Variable for checking data*/
	 year=SUBSTR(source, LENGTH(source)-3);

     POOLWT = PERWT/2 ;  /* Pooled survey weight */

     /*Create a dichotomous SUBPOP variable 
	   (POPULATION WITH AGE=26-30, UNINSURED WHOLE YEAR, AND HIGH INCOME)
	 */
     IF 26 LE AGELAST LE 30 AND POVCAT=5 AND INSCOV=3 THEN SUBPOP=1;
     ELSE SUBPOP=2;
     
RUN;

ODS HTML CLOSE; /*This will make the default HTML output no longer active,
                  and the output will not be displayed in the Results Viewer.*/
ODS LISTING ;  /*Open the listing destination */
TITLE "COMBINED MEPS DATA FROM 2016 and 2017 Consolidated Files";
PROC SORT DATA=WORK.POOL; BY YEAR SUBPOP; RUN;
PROC FREQ DATA=WORK.POOL;
    BY YEAR SUBPOP;
   	TABLES AGELAST*POVCAT*INSCOV/ LIST MISSING ;
	TABLES POVCAT*INSCOV/ LIST MISSING ;
	FORMAT AGELAST AGE. POVCAT POVCAT. INSCOV INSF.;
RUN;
PROC MEANS DATA=POOL N NMISS;
RUN;


ods graphics off; /*Suppress the graphics */
TITLE2 'WEIGHTED ESTIMATE FOR OUT-OF-POCKET EXPENSES FOR PERSONS AGES 26-30, UNINSURED WHOLE YEAR, AND HIGH INCOME';

ODS GRAPHICS OFF;
ods listing; /* Open the listing destination*/
ODS EXCLUDE STATISTICS; /* Not to generate output for the overall population */
/* PROC SURVEYMEANS computes the NOBS, MEANS, STDERR, and CLM statistics by default */
PROC SURVEYMEANS DATA=WORK.POOL; 
    VAR  TOTSLF;
    STRATUM VARSTR ;
	CLUSTER VARPSU ;
	WEIGHT  POOLWT;
	DOMAIN  SUBPOP("AGE 26-30, UNINSURED WHOLE YEAR, AND HIGH INCOME");
    FORMAT SUBPOP SUBPOP.;
RUN;

/* THE PROC PRINTTO null step is required to close the PROC PRINTTO, 
 only if used earlier */
PROC PRINTTO;
RUN;
 
