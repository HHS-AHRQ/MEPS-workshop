/**********************************************************************************

PROGRAM:      EXERCISE4.SAS

DESCRIPTION:  THIS PROGRAM ILLUSTRATES HOW TO POOL MEPS LONGITUDINAL DATA FILES FROM DIFFERENT PANELS
              THE EXAMPLE USED IS PANELS 17-19 POPULATION AGE 26-30 WHO ARE UNINSURED BUT HAVE HIGH INCOME IN THE FIRST YEAR

	            DATA FROM PANELS 17, 18, AND 19 ARE POOLED.

INPUT FILE:     (1) C:\MEPS\SAS\DATA\H183.SAS7BDAT (PANEL 19 LONGITUDINAL FILE)
	            (2) C:\MEPS\SAS\DATA\H172.SAS7BDAT (PANEL 18 LONGITUDINAL FILE)
	            (3) C:\MEPS\SAS\DATA\H164.SAS7BDAT (PANEL 17 LONGITUDINAL FILE)

*********************************************************************************/;
/* IMPORTANT NOTE: Use the next 6 lines of code, only if you want to specify an 
alternative destination for SAS log and SAS procedure output. 
Otherwise comment  out those six statements */

%LET MyFolder= S:\CFACT\Shared\WORKSHOPS\2019\Spring2019\Exercise_4;
OPTIONS LS=132 PS=79 NODATE FORMCHAR="|----|+|---+=|-/\<>*" PAGENO=1;
FILENAME MYLOG "&MyFolder\Exercise4_log.TXT";
FILENAME MYPRINT "&MyFolder\Exercise4_OUTPUT.TXT";
PROC PRINTTO LOG=MYLOG PRINT=MYPRINT NEW;
RUN;

proc datasets lib=work nolist kill; quit; /* delete  all files in the WORK library */
LIBNAME CDATA 'C:\DATA';
PROC FORMAT;
	VALUE POVCAT 
    1 = '1 POOR/NEGATIVE'
    2 = '2 NEAR POOR'
    3 = '3 LOW INCOME'
    4 = '4 MIDDLE INCOME'
    5 = '5 HIGH INCOME'
    ;

	VALUE INSF
	-1= '-1 INAPPLICABLE'
	1 = '1 ANY PRIVATE'
	2 = '2 PUBLIC ONLY'
	3 = '3 UNINSURED';

    VALUE AGE
	  -1= '-1 INAPPLICABLE'    
    26-30='26-30'
    0-25, 31-HIGH='0-25, 31+';

	VALUE  SUBPOP (max= 50)
	1 = 'AGE 26-30, UNINSURED WHOLE YEAR, AND HIGH INCOME'
	2 ='OTHERS';
run;

/* RENAME YEAR SPECIFIC VARIABLES PRIOR TO COMBINING FILES */
DATA WORK.POOL;
      SET CDATA.H164 (KEEP=DUPERSID INSCOVY1 INSCOVY2 LONGWT VARSTR VARPSU POVCATY1 AGEY1X PANEL)
	       CDATA.H172 (KEEP=DUPERSID INSCOVY1 INSCOVY2 LONGWT VARSTR VARPSU POVCATY1 AGEY1X PANEL)
	       CDATA.H183 (KEEP=DUPERSID INSCOVY1 INSCOVY2 LONGWT VARSTR VARPSU POVCATY1 AGEY1X PANEL);
     POOLWT = LONGWT/3 ;
   
     IF INSCOVY1=3 AND 26 LE AGEY1X LE 30 AND POVCATY1=5 THEN SUBPOP=1;
     ELSE SUBPOP=2;
  RUN;
ODS HTML CLOSE; /* This will make the default HTML output no longer active,
                  and the output will not be displayed in the Results Viewer.*/

TITLE1 ' ';
TITLE2 ' ';
TITLE3 '2019 AHRQ MEPS DATA USERS WORKSHOP (EXERCISE4.SAS)';
TITLE4 'COMBINED MEPS DATA FILES FROM DIFFERENT PANELS (PANELS 17, 18, and 19)';
TITLE5 "CHECK MISSING VALUES ON THE COMBINED DATA";
PROC MEANS DATA=POOL N NMISS;
RUN;

TITLE5 'SUPPORTING CROSSTAB FOR THE CREATION OF THE SUBPOP FLAG';
PROC FREQ DATA=POOL;
TABLES SUBPOP SUBPOP*PANEL SUBPOP*INSCOVY1*AGEY1X*POVCATY1/LIST MISSING;
FORMAT AGEY1X AGE. POVCATY1 POVCAT. INSCOVY1 INSF. SUBPOP SUBPOP.;
RUN;
ODS GRAPHICS OFF;
ods listing; /* Open the listing destination*/
TITLE5 'INSURANCE STATUS IN THE SECOND YEAR FOR THOSE W/ AGE=26-30, UNINSURED WHOLE YEAR, AND HIGH INCOME IN THE FIRST YEAR';
TITLE6 "AUTOMATIC OUTPUT GENERATED FROM PROC SURVEYMEANS";
PROC SURVEYMEANS DATA=POOL NOBS MEAN STDERR;
	STRATUM VARSTR ;
	CLUSTER VARPSU ;
	WEIGHT  POOLWT;
	VAR  INSCOVY2;
	CLASS INSCOVY2;
    FORMAT INSCOVY2 INSF. SUBPOP SUBPOP.;
	DOMAIN  SUBPOP("AGE 26-30, UNINSURED WHOLE YEAR, AND HIGH INCOME");
	ODS OUTPUT DOMAIN=work.domain_results;
RUN;

TITLE6 "CUSTOMIZED OUTPUT BASED ON ODS TABLE NAME (DOMAIN OUTPUT DATA SET) CREATED FROM PROC SURVEYMEANS";
proc print data= work.domain_results noobs split='*';
 var   VARLEVEL N  mean StdErr  ;
 sum N mean;
 label SUBPOP='POPULATION WITH AGE 26-30, UNINSURED, AND HIGH INCOME IN FIRST YEAR'
       INSCOVY2="HEALTH INSURANCE COVERAGE INDICATOR IN YEAR 2"
       mean = 'Proportion'
       StdErr = 'SE of Proportion';
       format N Comma12. mean comma9.3 stderr 9.6;  
where  N ne 0 ;
run;

/* THE PROC PRINTTO null step is required to close the PROC PRINTTO, 
 only if used earlier */
PROC PRINTTO;
RUN;

