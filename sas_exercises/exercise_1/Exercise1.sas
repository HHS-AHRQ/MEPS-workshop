/**********************************************************************************

PROGRAM:      C:\MEPS\SAS\PROG\EXERCISE1.SAS

DESCRIPTION:  THIS PROGRAM GENERATES THE FOLLOWING ESTIMATES ON NATIONAL HEALTH CARE EXPENSES, 2016:

	           (1) OVERALL EXPENSES 
	           (2) PERCENTAGE OF PERSONS WITH AN EXPENSE
	           (3) MEAN EXPENSE PER PERSON WITH AN EXPENSE


INPUT FILE:   C:\MEPS\SAS\DATA\H192.SAS7BDAT (2016 FULL-YEAR FILE)

*********************************************************************************/;
OPTIONS LS=132 PS=79 NODATE;
ods graphics off;

*LIBNAME CDATA 'C:\MEPS\SAS\DATA';
*LIBNAME CDATA "\\programs.ahrq.local\programs\MEPS\AHRQ4_CY2\B_CFACT\BJ001DVK\Workshop_2018_Fall\SAS\DATA";

/* LOAD SAS TRANSPORT FILES (.ssp) */
FILENAME in_h192 'C:\MEPS\h192.ssp';
proc xcopy in = in_h192 out = WORK IMPORT;
run;

/* CREATE FORMATS */
PROC FORMAT;
  VALUE AGEF
     0-  64 = '0-64'
     65-HIGH = '65+'
	;

  VALUE AGECAT
	   1 = '0-64'
	   2 = '65+'
	;

  VALUE GTZERO
     0         = '0'
     0 <- HIGH = '>0'
	;
RUN;

TITLE1 '2018 AHRQ MEPS DATA USERS WORKSHOP';
TITLE2 "EXERCISE1.SAS: NATIONAL HEALTH CARE EXPENSES, 2016";

/* READ IN DATA FROM 2016 CONSOLIDATED DATA FILE (HC-192) */
DATA PUF192;
  SET h192 (KEEP= TOTEXP16 AGE16X AGE42X AGE31X VARSTR VARPSU PERWT16F);

  TOTAL = TOTEXP16;

  /* CREATE FLAG (1/0) VARIABLES FOR PERSONS WITH AN EXPENSE */  
  X_ANYSVCE=0;
  IF TOTAL > 0 THEN X_ANYSVCE=1;

  /* CREATE A SUMMARY VARIABLE FROM END OF YEAR, 42, AND 31 VARIABLES*/

       IF AGE16X >= 0 THEN AGE = AGE16X ;
  ELSE IF AGE42X >= 0 THEN AGE = AGE42X ;
  ELSE IF AGE31X >= 0 THEN AGE = AGE31X ;

       IF 0 LE AGE LE 64 THEN AGECAT=1 ;
  ELSE IF      AGE  > 64 THEN AGECAT=2 ;
RUN;

TITLE3 "Supporting crosstabs for the flag variables";
PROC FREQ DATA=PUF192;
   TABLES X_ANYSVCE              * TOTAL
          AGECAT*AGE
          /LIST MISSING;
   FORMAT TOTAL        	gtzero.      
          AGE            agef.
     ;
RUN;

TITLE3 'OVERALL EXPENSES'; ;
PROC SURVEYMEANS DATA=PUF192 MEAN STDERR SUM STD;
	STRATUM VARSTR;
	CLUSTER VARPSU;
	WEIGHT PERWT16F;
	VAR    TOTAL ;
RUN;

TITLE3 'PERCENTAGE OF PERSONS WITH AN EXPENSE'; 
PROC SURVEYMEANS DATA= PUF192 MEAN STDERR SUM STD;
	STRATUM VARSTR ;
	CLUSTER VARPSU ;
	WEIGHT  PERWT16F ;
	VAR X_ANYSVCE; 
RUN;

TITLE3 'MEAN EXPENSE PER PERSON WITH AN EXPENSE, FOR OVERALL, AGE 0-64, AND AGE 65+';
PROC SURVEYMEANS DATA= PUF192 MEAN NOBS SUMWGT STDERR SUM STD;
	STRATUM VARSTR ;
	CLUSTER VARPSU ;
	WEIGHT  PERWT16F ;	
	VAR  TOTAL;
	DOMAIN  X_ANYSVCE('1') X_ANYSVCE('1')*AGECAT ;
	FORMAT  AGECAT agecat.;
RUN;

